compile_unit ->
		  stmt_sequence

stmt_sequence ->
		  stmt_sequence stmt
		| stmt
		| Îµ



stmt ->
		  ';'
		| if_stmt
		| while_stmt
		| for_stmt
		| expr_stmt
		| BREAK ';'
		| CONTINUE ';'
		| RETURN ';'
		| RETURN expr_list ';'

if_stmt ->
		  IF expr THEN stmt_sequence END
		| IF expr THEN stmt_sequence ELSE stmt_sequence END

while_stmt ->
		  WHILE expr THEN stmt_sequence END

for_stmt ->
		  FOR expr ';' expr ';' expr THEN stmt_sequence END

expr_stmt ->
		  expr ';'



expr_list ->
		  expr ',' expr_list
		| expr
prefix_expr_list ->
		  prefix_expr ',' prefix_expr_list
		| prefix_expr



expr ->
		  assign_expr

assign_expr ->
		  prefix_expr_list assign_op expr_list
		| logical_expr

logical_expr -> 
		  NOT compare_expr
		| compare_expr logical_op logical_expr
		| compare_expr

compare_expr ->
		  addtive_expr compare_op compare_expr
		| addtive_expr

addtive_expr -> 
		  multiply_expr addtive_op addtive_expr
		| multiply_expr

multiply_expr ->
		  prefix_expr multiply_op multiply_expr
		| prefix_expr

prefix_expr ->
		  prefix_op suffix_expr
		| suffix_expr

suffix_expr ->
		  single_expr suffix_op
		| single_expr

single_expr ->
		  '(' expr ')'
		| NIL
		| FALSE
		| TRUE
		| immediate
		| IDENTIFIER


suffix_op ->
		  '(' ')' suffix_op
		| '(' ')'
		| '(' expr_list ')' suffix_op
		| '(' expr_list ')'
		| '[' expr ']' suffix_op
		| '[' expr ']'
		| '.' IDENTIFIER suffix_op
		| '.' IDENTIFIER


immediate ->
		  object_immediate
		| function_define
		| STRING_IMMEDIATE
		| NUMBER_IMMEDIATE
object_immediate ->
		  '{' '}'
		| '{' object_immediate_item_list '}'
object_immediate_item_list ->
		  object_immediate_item ',' object_immediate_item_list
		| object_immediate_item
object_immediate_item ->
		  IDENTIFIER ':' object_immediate_item_value
		| STRING_IMMEDIATE ':' object_immediate_item_value
		| NUMBER_IMMEDIATE ':' object_immediate_item_value
object_immediate_item_value ->
		  immediate

function_define ->
		  FUNCTION '(' identifier_list ')' stmt_sequence END
identifier_list ->
		  IDENTIFIER
		| IDENTIFIER ',' identifier_list


assign_op ->
		  '='
		| '+='
		| '-='
		| '*='
		| '/='

logical_op ->
		  AND
		| OR

compare_op ->
		  '<'
		| '>'
		| '<='
		| '>='
		| '=='
		| '!='

addtive_op ->
		  '+'
		| '-'

multiply_op ->
		  '*'
		| '/'

prefix_op ->
		  '-'
